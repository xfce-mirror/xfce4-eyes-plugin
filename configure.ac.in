dnl xfce4-eyes-plugin - eyes that spy on you
dnl

dnl ***************************
dnl *** Version information ***
dnl ***************************
m4_define([eyes_version_major], [4])
m4_define([eyes_version_minor], [6])
m4_define([eyes_version_micro], [0])
m4_define([eyes_version_build], [r@REVISION@])
m4_define([eyes_version_tag], [])
m4_define([eyes_version], [eyes_version_major().eyes_version_minor().eyes_version_micro()ifelse(eyes_version_tag(), [git], [eyes_version_tag()-eyes_version_build()], [eyes_version_tag()])])

dnl ***************************
dnl *** Initialize autoconf ***
dnl ***************************
AC_COPYRIGHT([Copyright (c) 2006-2020 The Xfce development team])
AC_INIT([xfce4-eyes-plugin], [eyes_version], [https://gitlab.xfce.org/panel-plugins/xfce4-eyes-plugin/], [xfce4-eyes-plugin])
AC_PREREQ([2.60])
AC_CONFIG_MACRO_DIRS([m4])
AC_CANONICAL_TARGET()

dnl ***************************
dnl *** Initialize automake ***
dnl ***************************
AM_INIT_AUTOMAKE([1.9 dist-bzip2 tar-ustar no-dist-gzip foreign])
AC_CONFIG_HEADERS([config.h])
AM_MAINTAINER_MODE()
m4_ifdef([AM_SILENT_RULES], [AM_SILENT_RULES([yes])])

dnl ********************************
dnl *** Check for basic programs ***
dnl ********************************
AC_PROG_CC()
AC_PROG_LD()
AC_PROG_INSTALL()
LT_PREREQ([2.2.6])
LT_INIT([disable-static])
IT_PROG_INTLTOOL([0.35.0])
AM_PROG_CC_C_O()

dnl **********************************
dnl *** Check for standard headers ***
dnl **********************************
AC_CHECK_HEADERS([math.h])

dnl ******************************
dnl *** Check for math library ***
dnl ******************************
AC_CHECK_LIB([m], [sin])

dnl ******************************
dnl *** Check for i18n support ***
dnl ******************************
XDT_I18N([@LINGUAS@])

dnl ***********************************
dnl *** Check for required packages ***
dnl ***********************************
XDT_CHECK_PACKAGE([GTK], [gtk+-3.0], [3.14.0])
XDT_CHECK_PACKAGE([LIBXFCE4UI], [libxfce4ui-2], [4.12.0])
XDT_CHECK_PACKAGE([LIBXFCE4PANEL], [libxfce4panel-2.0], [4.12.0])

dnl ***********************************
dnl *** Check for debugging support ***
dnl ***********************************
XDT_FEATURE_DEBUG()

AC_CONFIG_FILES([
Makefile
po/Makefile.in
panel-plugin/Makefile
icons/Makefile
icons/48x48/Makefile
icons/36x36/Makefile
icons/32x32/Makefile
icons/24x24/Makefile
icons/22x22/Makefile
icons/16x16/Makefile
themes/Makefile
themes/Bizarre/Makefile
themes/Bloodshot/Makefile
themes/Default/Makefile
themes/Default-tiny/Makefile
themes/Horrid/Makefile
themes/Tango/Makefile
themes/Chibi/Makefile
])
AC_OUTPUT
